# Maintainer: Anurag Roy <anuragr9847@gmail.com>
_pkgname="dwm"
pkgname="$_pkgname-royarg-git"
pkgver=6.4.r11.eec38cc
pkgrel=1
pkgdesc="A modified version of the dynamic window manager for X."
arch=('i686' 'x86_64')
url="https://github.com/royarg02/$_pkgname"
license=('MIT')
depends=('dmenu-royarg-git')
makedepends=('git')
optdepends=('st: A simple virtual terminal emulator for X.')
provides=("$_pkgname")
conflicts=("$_pkgname" "$_pkgname-git")
source=("git+$url.git")
md5sums=('SKIP')

pkgver() {
  cd "$_pkgname"
  git describe --long --tags | sed 's/\([^-]*-\)g/r\1/;s/-/./g'
}

build() {
  cd "$_pkgname"
  make \
    X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd "$_pkgname"
  make PREFIX=/usr DESTDIR="$pkgdir" install
  install -Dm644 README "$pkgdir"/usr/share/doc/$pkgname/README
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
