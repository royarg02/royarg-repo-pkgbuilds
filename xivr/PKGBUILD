# Maintainer: Anurag Roy <anuragr9847@gmail.com>
pkgname="xivr"
pkgver=1.1.2
pkgrel=1
pkgdesc='Wrapper for *sxiv with nice features'
arch=('x86_64')
url="https://github.com/royarg02/${pkgname}"
license=('GPL-3.0-or-later')
depends=('util-linux')
makedepends=('git' 'dart')
optdepends=('nsxiv: default image viewer'
  'sxiv: alternative image viewer'
)
options=(!strip !debug)
source=("git+${url}#tag=${pkgver}"
        "xivr.desktop")
sha256sums=('97f764257306f8d3e8eb756b84a56751af4cb0f46c467b08888f0d4d7f561c85'
            '3b05b3fbbda213d1c67d790b9b098ec962ca265b412f7e1b1024a1f16a3b583a')

prepare() {
  export PUB_CACHE="${srcdir}/pub-cache"
  dart pub get --directory="${pkgname}" --no-offline --no-precompile --suppress-analytics
}

build() {
  cd ${pkgname}
  VERSION="$(git describe --tags --abbrev=0)"
  [ ! -d build ] && mkdir build
  dart compile exe -DVERSION="$VERSION" bin/xivr.dart -o build/xivr --suppress-analytics
}

package() {
  install -Dm755 -t "${pkgdir}/usr/bin" "${pkgname}/build/xivr"
  install -Dm644 -t "${pkgdir}/usr/share/applications" "${srcdir}/xivr.desktop"
  install -Dm644 -t "${pkgdir}/usr/share/licenses/${pkgname}" "${pkgname}/LICENSE"
}

