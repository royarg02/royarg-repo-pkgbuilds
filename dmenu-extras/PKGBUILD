# Maintainer: Anurag Roy <anuragr9847@gmail.com>
pkgname="dmenu-extras"
pkgver=1.0.r5.262e78e
pkgrel=3
pkgdesc="Helper dmenu scripts for basic tasks"
arch=(any)
url="https://github.com/royarg02/$pkgname"
license=('GPL-3.0-or-later')
depends=('sh' 'dmenu' 'curl' 'gtk3' 'xclip' 'imagemagick' 'jq' 'xdg-utils')
makedepends=('git')
optdepends=('libnotify: notifications for scripts'
  'slop: region selection for dm-scshot'
  'xorg-xprop: window selection for dm-scshot'
)
conflicts=("$pkgname-git")
source=("git+$url.git")
md5sums=('SKIP')
options=(!debug)

pkgver() {
  cd "${pkgname}"
  printf "1.0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd ${pkgname}
  for i in $(ls dm-*); do
    install -Dm755 "${i}" -t "${pkgdir}/usr/bin"
  done
  install -Dm644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  # Properly install symlinks
  rm -f "${pkgdir}/usr/bin/dm-modify-bookmark" && \
  ln -s "/usr/bin/dm-bookmark" "${pkgdir}/usr/bin/dm-modify-bookmark"
}
